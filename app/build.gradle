apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31
    apply from: '../dependencies/signing.gradle', to: android
    def globalConfiguration = rootProject.extensions.getByName("ext")

    defaultConfig {
        applicationId "net.ambitious.android.sharebookmarks"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode globalConfiguration["appVersionCode"]
        versionName globalConfiguration["appVersionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
        manifestPlaceholders = [
                admob_key: globalConfiguration["adMobCode"]
        ]
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
            buildConfigField "String", "AD_MOB_BANNER_KEY", "\"" + globalConfiguration["adMobBannerKey"] + "\""
            buildConfigField "String", "CONTACT_URL", "\"" + globalConfiguration["inquiryUrl"] + "\""
            buildConfigField "String", "BASE_DOMAIN", "\"http://192.168.11.3:8080/\""
        }
        staging {
            debuggable true
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            signingConfig signingConfigs.staging
            buildConfigField "String", "AD_MOB_BANNER_KEY", "\"" + globalConfiguration["adMobBannerKey"] + "\""
            buildConfigField "String", "CONTACT_URL", "\"" + globalConfiguration["inquiryUrl"] + "\""
            buildConfigField "String", "BASE_DOMAIN", "\"" + globalConfiguration["stagingDomain"] + "\""
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "AD_MOB_BANNER_KEY", "\"" + globalConfiguration["adMobBannerKey"] + "\""
            buildConfigField "String", "CONTACT_URL", "\"" + globalConfiguration["inquiryUrl"] + "\""
            buildConfigField "String", "BASE_DOMAIN", "\"" + globalConfiguration["releaseDomain"] + "\""
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    def kotlin_version = captureVersion(implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.31"))
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    def navigation_version = captureVersion(implementation("androidx.navigation:navigation-fragment-ktx:2.3.5"))
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    def room_version = captureVersion(implementation("androidx.room:room-runtime:2.3.0"))
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-guava:$room_version"
    implementation "com.google.android.gms:play-services-ads:20.4.0"
    implementation "com.google.firebase:firebase-crashlytics:18.2.3"
    implementation "com.google.firebase:firebase-analytics:20.0.0"
    implementation "com.google.firebase:firebase-config:21.0.1"
    implementation "com.google.firebase:firebase-messaging:23.0.0"
    implementation 'com.google.android.gms:play-services-basement:17.6.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    def koin_version = captureVersion(implementation("org.koin:koin-androidx-scope:2.1.6"))
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "com.firebaseui:firebase-ui-auth:6.2.0"
    implementation "com.github.bumptech.glide:glide:4.12.0"
    def coroutines_version = captureVersion(implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"))
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    def okhttp_version = captureVersion(implementation("com.squareup.okhttp3:okhttp:4.9.2"))
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    def moshi_version = captureVersion(implementation("com.squareup.moshi:moshi:1.12.0"))
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    def retrofit_version = captureVersion(implementation("com.squareup.retrofit2:retrofit:2.9.0"))
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "joda-time:joda-time:2.10.13"
    implementation "org.jsoup:jsoup:1.14.3"
    implementation "com.airbnb.android:lottie:4.2.0"
    implementation "jp.wasabeef:recyclerview-animators:4.0.2"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "org.koin:koin-test:$koin_version"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

apply plugin: 'com.google.gms.google-services'
